Letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" .
Digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .
Number = Digit {Digit} .
Symbol = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">" | "=" | "|" | "." | "," | ";" .
WhiteSpace = " " .
ArrayName = Letter {Letter | Digit | "-" | "_"} .
DeviceName = Letter {Letter | Digit | "-" | "_"} . 
DeviceType = "uram" | "pram" .
DeviceNameList = DeviceName [{"," DeviceName}] .
VolumeName = Letter {Letter | Digit | "-" | "_"} . 
VolumeNameList = VolumeName [{"," VolumeName}] . 
TargetNVMSubsystemNVMeQualifiedName = Letter {Letter | Digit | "-" | "_"} .
RaidType = ["RAID5" | "raid5"] .
SubsystemNQN = Letter {Letter | Digit | "-" | "_"} . 

VolumeSize = Number ("B" | "K" | "KiB" | "KB" | "G" | "GiB" | "GB" | "T" | "TiB" | "TB" | "P" | "PiB" | "PB" | "E" | "EiB" | "EB") .
IOPS = Number .
Bandwidth = Number "MB/s".

ApplicationName = "poseidonos-cli" . 
Cmd = ApplicationName WhiteSpace (ArrayCmd | VolumeCmd | DeviceCmd | SystemCmd | LoggerCmd | SubsystemCmd | QosCmd) .

ArrayCmd = "array" WhiteSpace (MountArrayCmd | UnmountArrayCmd | ListArrayCmd | AddSpareCmd | RemoveSpareCmd | DeleteArrayCmd | CreateArrayCmd | AutoCreateArrayCmd) .
MountArrayCmd = "mount" WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName .
UnmountArrayCmd = "unmount" WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName ["--force"] .
ListArrayCmd = "list" [WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName] .
AddSpareCmd = "addspare" WhiteSpace ("--spare" | "-s") WhiteSpace DeviceName ("--array-name" | "-a") WhiteSpace ArrayName .
RemoveSpareCmd = "rmspare" WhiteSpace ("--spare" | "-s") WhiteSpace DeviceName ("--array-name" | "-a")  WhiteSpace ArrayName ["--force"] .
CreateArrayCmd = "create" ("--array-name" | "-a") WhiteSpace ArrayName WhiteSpace ("--buffer" | "-b") WhiteSpace DeviceName WhiteSpace ("--data-devs" | "-d") WhiteSpace DeviceNameList [WhiteSpace ("--spare" | "-s") WhiteSpace DeviceName] [WhiteSpace "--raid" WhiteSpace RaidType] .
DeleteArrayCmd = "delete" WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName ["--force"] .
AutoCreateArrayCmd = "autocreate" WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName WhiteSpace ("--num-buffer" | "-b") WhiteSpace Number WhiteSpace ("--num-data-devs" | "-d") WhiteSpace Number WhiteSpace [("--num-spare" | "-s") WhiteSpace Number] WhiteSpace ["--raid" WhiteSpace RaidType] .

VolumeCmd = "volume" WhiteSpace (VolumeCreateCmd | VolumeDeleteCmd | VolumeMountCmd | VolumeUnmountCmd | VolumeListCmd | VolumeSetPropertyCmd | VolumeRenameCmd) .
VolumeCreateCmd = "create" WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeName WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName WhiteSpace "--size" WhiteSpace VolumeSize [WhiteSpace "--maxiops" WhiteSpace IOPS] [WhiteSpace "--maxbw" WhiteSpace Bandwidth] .
VolumeDeleteCmd = "delete" WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeName WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName ["--force"] .
VolumeMountCmd = "mount" WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeName WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName [WhiteSpace "--subnqn" WhiteSpace TargetNVMSubsystemNVMeQualifiedName] .
VolumeUnmountCmd = "unmount" WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeName WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName ["--force"] .
VolumeListCmd = "list" [WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName] .
VolumeRenameCmd = "rename" WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeName WhiteSpace ("--new-volume-name" | -"n) WhiteSpace VolumeName WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName .

DeviceCmd = "device" WhiteSpace (CreateDeviceCmd | ScanDeviceCmd | ListDeviceCmd | SMARTCmd) . 
CreateDeviceCmd = "create" WhiteSpace ("--device-name" | "-d") DeviceName WhiteSpace "--num-blocks" Whitespace {Digit} WhiteSpace "--block-size" WhiteSpace {Digit} WhiteSpace "--device-type" WhiteSpace DeviceType.
ScanDeviceCmd = "scan" . 
ListDeviceCmd = "list" . 
SMARTCmd = "smart" WhiteSpace ("--device-name" | "-d") WhiteSpace DeviceName .

SystemCmd = "system" WhiteSpace (StartSystemCmd | StopSystemCmd | SystemInfoCmd | SetSystemPropertyCmd) .
StartSystemCmd = "start" .
StopSystemCmd = "stop" ["--force"] .
SystemInfoCmd = "info" .
SetSystemPropertyCmd = "set-property" [WhiteSpace "--rebuild-impact" WhiteSpace ("high" | "low")] .

LoggerCmd = "logger" (SetLogLevelCmd | GetLogLeveCmd | ApplyLogFilterCmd | LoggerInfoCmd) .
SetLogLevelCmd = "set-level" WhiteSpace "--level" LogLevel . 
LogLevel = "error" | "debug" | "warn" | "err" | "critical" .
GetLogLeveCmd = "get-level" .
ApplyLogFilterCmd = "apply-filter" .
LoggerInfoCmd = "info" .

QosCmd = "qos" WhiteSpace (QosVolumePolicyCmd | QosVolumeResetCmd | QosListCmd) .
QosVolumePolicyCmd = "create" WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeNameList WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName [WhiteSpace "--miniops" WhiteSpace IOPS] [WhiteSpace "--maxiops" WhiteSpace IOPS] [WhiteSpace "--minbw" WhiteSpace Bandwidth] [WhiteSpace "--maxbw" WhiteSpace Bandwidth] .
QosVolumeResetCmd = "reset" WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeNameList WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName .
QosListCmd = "list" [WhiteSpace ("--volume-name" | "-v") WhiteSpace VolumeNameList] [WhiteSpace ("--array-name" | "-a") WhiteSpace ArrayName] .

SubsystemCmd = "subsystem" WhiteSpace (DeleteSubsystemCmd) .
DeleteSubsystemCmd = "delete" WhiteSpace "--subnqn" WhiteSpace SubsystemNQN ["--force"] .
